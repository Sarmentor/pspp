dnl PSPP - a program for statistical analysis.
dnl Copyright (C) 2017 Free Software Foundation, Inc.
dnl
dnl This program is free software: you can redistribute it and/or modify
dnl it under the terms of the GNU General Public License as published by
dnl the Free Software Foundation, either version 3 of the License, or
dnl (at your option) any later version.
dnl
dnl This program is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.
dnl
dnl You should have received a copy of the GNU General Public License
dnl along with this program.  If not, see <http://www.gnu.org/licenses/>.
dnl
AT_BANNER([output drivers])

m4_define([OUTPUT_UNWRITABLE_DIR],
  [AT_SETUP([output $1 to unwritable directory])
   mkdir unwritable
   chmod u-w unwritable
   AT_SKIP_IF([: > unwritable/test])
   AT_DATA([unwritable.sps], [dnl
data list /x 1.
begin data.
1
2
3
end data.
frequencies x/histogram.
])
   dnl PSPP will fail to create the output file.  Thus, the exit status is
   dnl non zero
   AT_CHECK([cd unwritable && pspp -o pspp.$1 ../unwritable.sps], [1],
            [ignore], [ignore])
   AT_CLEANUP])

OUTPUT_UNWRITABLE_DIR([csv])
OUTPUT_UNWRITABLE_DIR([html])
OUTPUT_UNWRITABLE_DIR([odt])
OUTPUT_UNWRITABLE_DIR([pdf])
OUTPUT_UNWRITABLE_DIR([txt])
